plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id "org.springframework.boot" version '2.0.3.RELEASE'
}

ext {
	// Has to be 1.16.20 as 1.18 failed with gradle 4.9 & jdk8
	lombokVersion = '1.16.20'	
	vaadinVersion = '10.0.1'
}


apply plugin: 'java'
apply plugin: 'eclipse'
// Eclipse configuration (from eclipse plugin)
eclipse {
	// always download sources and docs
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

apply plugin: 'org.springframework.boot'
springBoot {
	mainClassName = 'de.eldar.vacationhome.VacationHomeApplication'
}

apply plugin: 'io.spring.dependency-management'
// Implicitly loaded by boot plugin
dependencyManagement {
	imports {
		mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
	}
}

apply plugin: "io.franzbecker.gradle-lombok"
lombok {
	version = lombokVersion
	// Dont check the hash
	sha256 = ""
}

repositories {
	mavenCentral()	
	maven { url "https://maven.vaadin.com/vaadin-addons" }
}

dependencies {	
	// Vaadin spring boot starter (version from BOM)
	compile("com.vaadin:vaadin-spring-boot-starter")
	compile("com.vaadin:vaadin-cookie-consent-flow:1.0.0")	
	
	compile("org.webjars.bowergithub.polymerelements:paper-input:2.2.3")
	compile("org.webjars.bowergithub.polymerelements:iron-form:2.1.3")
	compile("org.webjars.bowergithub.polymerelements:iron-pages:2.1.1")
	
	//JUnit (version from spring BOM)
	testCompile("junit:junit")	
	// Spring boot test support (version from spring BOM)
	testCompile("org.springframework.boot:spring-boot-starter-test")
	
	// Lombok 
	compileOnly("org.projectlombok:lombok:${lombokVersion}")	
}

group = 'de.eldar'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
